import java.text.SimpleDateFormat

plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'demo'
version = new Version('1.0.0-xxx')

repositories {
  mavenCentral()
}


ext {
  vertxVersion = '3.4.2'
  mainVerticleName = 'com.example.starter.MainVerticle'
  launcherClassName = 'io.vertx.core.Starter'
}


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

allprojects {
  tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
  }
  tasks.withType(Test).configureEach {
    systemProperty 'file.encoding', 'UTF-8'
  }
}

application {
  mainClass = launcherClassName
}

dependencies {
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"

  testImplementation "io.vertx:vertx-unit:$vertxVersion"
}

shadowJar {
  archiveBaseName.set('starterVertx')
  archiveClassifier.set('')
  archiveVersion.set(project.version.toString())

  manifest {
    attributes(
      'Main-Class': launcherClassName,
      'Main-Verticle': mainVerticleName
    )
  }

  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

class Version {
  String thisVersion
  Date buildTime

  Version(String versionValue) {
    buildTime = new Date()
    thisVersion = versionValue.replace('xxx', getTimestamp())
  }

  String getTimestamp() {
    def format = new SimpleDateFormat('yyyyMMddHHmmss')
    format.setCalendar(
      Calendar.getInstance(TimeZone.getTimeZone('Asia/Ho_Chi_Minh'))
    )
    return format.format(buildTime)
  }

  String toString() {
    thisVersion
  }
}
