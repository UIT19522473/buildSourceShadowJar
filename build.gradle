import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import java.text.SimpleDateFormat

plugins {
  id 'java'
  id 'application'
  id 'com.gradleup.shadow' version '9.2.2'
}

group = 'com.example'
version = new BuildVersion('1.0.0-xxx')

repositories {
  mavenCentral()
}

ext {
  vertxVersion = '4.5.24'
  junitJupiterVersion = '5.9.1'

  mainVerticleName = 'com.example.starter.MainVerticle'
  launcherClassName = 'io.vertx.core.Launcher'

  watchForChange = 'src/**/*'
  doOnChange = "${projectDir}/gradlew classes"
}

/* =====================
   APPLICATION
   ===================== */
application {
  mainClass = launcherClassName
}

/* =====================
   DEPENDENCIES
   ===================== */
dependencies {
  implementation platform("io.vertx:vertx-stack-depchain:$vertxVersion")
  implementation 'io.vertx:vertx-web-client'
  implementation 'io.vertx:vertx-web'

  testImplementation 'io.vertx:vertx-junit5'
  testImplementation "org.junit.jupiter:junit-jupiter:$junitJupiterVersion"
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

/* =====================
   JAVA
   ===================== */
java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(ShadowJar).configureEach {
  // version has timestamp
  archiveVersion = project.version.toString()

  // <project>-<version>.jar
  archiveFileName = "${archiveBaseName.get()}-${archiveVersion.get()}.jar"

  manifest {
    attributes(
      'Main-Class': launcherClassName,
      'Main-Verticle': mainVerticleName
    )
  }

  mergeServiceFiles()
}


tasks.withType(JavaExec) {
  args = [
    'run',
    mainVerticleName,
    "--redeploy=$watchForChange",
    "--launcher-class=$launcherClassName",
    "--on-redeploy=$doOnChange"
  ]
}

class BuildVersion {
  String value
  Date buildTime = new Date()

  BuildVersion(String raw) {
    this.value = raw.replace('xxx', timestamp())
  }

  String timestamp() {
    def fmt = new SimpleDateFormat('yyyyMMddHHmmss')
    fmt.setCalendar(
      Calendar.getInstance(TimeZone.getTimeZone('Asia/Ho_Chi_Minh'))
    )
    fmt.format(buildTime)
  }

  String toString() {
    value
  }
}
